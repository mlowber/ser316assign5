plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.14'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

application {
    mainClass = 'Main'
}

checkstyle {
    toolVersion '10.3'
    configFile = file("config/checkstyle/checkstyle.xml")
}

spotbugs {
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'high'
}

tasks.named('check') {
    dependsOn 'spotbugsMain'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

jacoco {
    toolVersion = "0.8.8"
}

tasks.test {
    finalizedBy tasks.jacocoTestReport
}

tasks.jacocoTestReport {
    dependsOn tasks.test

    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    '**/Main.class',  // Exclude Main.java
                ])
            })
        )
    }
}
